plugins {
    id 'java-library'
    id 'groovy'
    id 'jacoco-report-aggregation'
    id 'maven-publish'
    id 'io.freefair.lombok' version '8.4'
}

group = 'it.fulminazzo'
version = '1.0.0'

repositories {
    mavenCentral()
    maven {
        name = 'fulminazzo'
        url = 'https://repo.fulminazzo.it/releases'
    }
}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.36'
    annotationProcessor 'org.projectlombok:lombok:1.18.36'
    compileOnly 'org.jetbrains:annotations:26.0.2'

    api 'it.fulminazzo:FulmiCollection:1.8'
    api 'it.fulminazzo:Configurations:1.6.3'

    testCompileOnly 'org.projectlombok:lombok:1.18.36'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.36'
    testCompileOnly 'org.jetbrains:annotations:26.0.2'

    testImplementation 'org.spockframework:spock-core:2.4-M4-groovy-4.0'
    testRuntimeOnly 'net.bytebuddy:byte-buddy:1.17.5'
    testImplementation 'org.mockito:mockito-core:4.11.0'
}

test {
    useJUnitPlatform()
}

testCodeCoverageReport {
    dependsOn test
    reports {
        xml.required = true
        csv.required = true
    }
}

tasks.register('sourcesJar', Jar) {
    from sourceSets.main.delombokTask
    archiveClassifier = 'sources'
}

tasks.register('javadocJar', Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }

    repositories {
        maven {
            url 'https://repo.fulminazzo.it/releases'
            credentials {
                username = System.getenv('REPO_USERNAME')
                password = System.getenv('REPO_PASSWORD')
            }
            authentication {
                basic(BasicAuthentication)
            }
        }
    }
}
